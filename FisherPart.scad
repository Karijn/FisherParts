
////////////////////////////////
staticHeight=2.8;
rrad=4;
vrad=.5;
staticMountHeigth=1.9;
staticLatDia=8;
staticLatRad=4;

gliderAsDiameter=6;

height=15; // [15, 30, 45, 60, 75, 90, 105, 120]
width=15; // [15, 30, 45, 60, 75, 90, 105, 120]
depth=15; // [15, 30, 45, 60, 75, 90, 105, 120]

rightAxes_Vertical = 0 ; // [0:None, 1:even 1, 2: even 2, 3:   even 3, 4:even 4, 5:even 5, 6:even 6, 7:even 7, 8: even 8, 9:even c4, 10:even c6, 11:even c8, 12:oddeven 2, 13:oddeven 3, 14:oddeven 4, 15:oddeven 5, 16:oddeven 6, 17:oddeven 7, 18:oddeven 8 ]

rightAxes_Horizontal = 0 ; // [0:None, 1:even 1, 2: even 2, 3:   even 3, 4:even 4, 5:even 5, 6:even 6, 7:even 7, 8: even 8, 9:even c4, 10:even c6, 11:even c8, 12:oddeven 2, 13:oddeven 3, 14:oddeven 4, 15:oddeven 5, 16:oddeven 6, 17:oddeven 7, 18:oddeven 8 ]

leftAxes_Vertical = 0 ; // [0:None, 1:even 1, 2: even 2, 3:   even 3, 4:even 4, 5:even 5, 6:even 6, 7:even 7, 8: even 8, 9:even c4, 10:even c6, 11:even c8, 12:oddeven 2, 13:oddeven 3, 14:oddeven 4, 15:oddeven 5, 16:oddeven 6, 17:oddeven 7, 18:oddeven 8 ]

leftAxes_Horizontal = 0 ; // [0:None, 1:even 1, 2: even 2, 3:   even 3, 4:even 4, 5:even 5, 6:even 6, 7:even 7, 8: even 8, 9:even c4, 10:even c6, 11:even c8, 12:oddeven 2, 13:oddeven 3, 14:oddeven 4, 15:oddeven 5, 16:oddeven 6, 17:oddeven 7, 18:oddeven 8 ]

axes_Top_Front_Back = 0 ; // [0:None, 1:even 1, 2: even 2, 3:   even 3, 4:even 4, 5:even 5, 6:even 6, 7:even 7, 8: even 8, 9:even c4, 10:even c6, 11:even c8, 12:oddeven 2, 13:oddeven 3, 14:oddeven 4, 15:oddeven 5, 16:oddeven 6, 17:oddeven 7, 18:oddeven 8 ]

axes_Bottom_Front_Back = 0 ; // [0:None, 1:even 1, 2: even 2, 3:   even 3, 4:even 4, 5:even 5, 6:even 6, 7:even 7, 8: even 8, 9:even c4, 10:even c6, 11:even c8, 12:oddeven 2, 13:oddeven 3, 14:oddeven 4, 15:oddeven 5, 16:oddeven 6, 17:oddeven 7, 18:oddeven 8 ]

axes_Top_Left_Right = 0 ; // [0:None, 1:even 1, 2: even 2, 3:   even 3, 4:even 4, 5:even 5, 6:even 6, 7:even 7, 8: even 8, 9:even c4, 10:even c6, 11:even c8, 12:oddeven 2, 13:oddeven 3, 14:oddeven 4, 15:oddeven 5, 16:oddeven 6, 17:oddeven 7, 18:oddeven 8 ]

axes_Bottom_Left_Right = 0 ; // [0:None, 1:even 1, 2: even 2, 3:   even 3, 4:even 4, 5:even 5, 6:even 6, 7:even 7, 8: even 8, 9:even c4, 10:even c6, 11:even c8, 12:oddeven 2, 13:oddeven 3, 14:oddeven 4, 15:oddeven 5, 16:oddeven 6, 17:oddeven 7, 18:oddeven 8 ]

frontAxes_Vertical = 0 ; // [0:None, 1:even 1, 2: even 2, 3:   even 3, 4:even 4, 5:even 5, 6:even 6, 7:even 7, 8: even 8, 9:even c4, 10:even c6, 11:even c8, 12:oddeven 2, 13:oddeven 3, 14:oddeven 4, 15:oddeven 5, 16:oddeven 6, 17:oddeven 7, 18:oddeven 8 ]

frontAxes_Horizontal = 0 ; // [0:None, 1:even 1, 2: even 2, 3:   even 3, 4:even 4, 5:even 5, 6:even 6, 7:even 7, 8: even 8, 9:even c4, 10:even c6, 11:even c8, 12:oddeven 2, 13:oddeven 3, 14:oddeven 4, 15:oddeven 5, 16:oddeven 6, 17:oddeven 7, 18:oddeven 8 ]

backAxes_Vertical = 0 ; // [0:None, 1:even 1, 2: even 2, 3:   even 3, 4:even 4, 5:even 5, 6:even 6, 7:even 7, 8: even 8, 9:even c4, 10:even c6, 11:even c8, 12:oddeven 2, 13:oddeven 3, 14:oddeven 4, 15:oddeven 5, 16:oddeven 6, 17:oddeven 7, 18:oddeven 8 ]

backAxes_Horizontal = 0 ; // [0:None, 1:even 1, 2: even 2, 3:   even 3, 4:even 4, 5:even 5, 6:even 6, 7:even 7, 8: even 8, 9:even c4, 10:even c6, 11:even c8, 12:oddeven 2, 13:oddeven 3, 14:oddeven 4, 15:oddeven 5, 16:oddeven 6, 17:oddeven 7, 18:oddeven 8 ]

squareHoles = false;

glidersLR_Vertical=[];
glidersFB_Vertical=[];
glidersLR_Horizontal=[];

rrect=1;

fastnerDia = 20;
fastnerHeight=5.5;
fastnerDoubleHeight=12;


function pyth(a, b) = sqrt((a*a)+(b*b));


/* [Hidden] */
square_Holes = 0 ; // [0:None, 1:even 1, 2: even 2, 3:   even 3, 4:even 4, 5:even 5, 6:even 6, 7:even 7, 8: even 8, 9:even c4, 10:even c6, 11:even c8, 12:oddeven 2, 13:oddeven 3, 14:oddeven 4, 15:oddeven 5, 16:oddeven 6, 17:oddeven 7, 18:oddeven 8 ]

fisherAsDiameter=4.2;


even_1 = [0];
even_2 = [-1, 1];
even_3 = [-2, 0, 2];
even_4 = [-3, -1, 1, 3];
even_5 = [-4, -2, 0, 2, 4];
even_6 = [-5, -3, -1, 1, 3, 5];
even_7 = [-6, -4, -2, 0, 2, 4, 6];
even_8 = [-7, -5, -3, -1, 1, 3, 5, 7];
even_c4 = [-3, -1, 0, 1, 3];
even_c6 = [-5, -3, -2, -1, 0, 1, 2, 3, 5];
even_c8 = [-7, -5, -3, -1, 0, 1, 3, 5, 7];

odd_1 = [];
odd_2 = even_1;
odd_3 = even_2;
odd_4 = even_3;
odd_5 = even_4;
odd_6 = even_5;
odd_7 = even_6;
odd_8 = even_7;
oddeven_1 = [0];
oddeven_2 = [-1, 0, 1];
oddeven_3 = [ -2, -1, 0, 1, 2];
oddeven_4 = [ -3, -2, -1, 0, 1, 2, 3];
oddeven_5 = [ -4, -3, -2, -1, 0, 1, 2, 3, 4];
oddeven_6 = [ -5, -4, -3, -2, -1, 0, 1, 2, 3, 4, 5];
oddeven_7 = [ -6, -5, -4, -3, -2, -1, 0, 1, 2, 3, 4, 5, 6];
oddeven_8 = [ -7, -6, -5, -4, -3, -2, -1, 0, 1, 2, 3, 4, 5, 6, 7];

function getAxes(axes) =  
    [
        odd_1, 
        even_1, 
        even_2,
        even_3,
        even_4,
        even_5,
        even_6,
        even_7,
        even_8,
        even_c4,
        even_c6,
        even_c8,
        oddeven_2,
        oddeven_3,
        oddeven_4,
        oddeven_5,
        oddeven_6,
        oddeven_7,
        oddeven_8
    ][axes];


function modulo(a,b) = [ for (c = a) if (len(search(c, b)) ) c ];

module roundedPlate(width=30, depth=0, height=2.8, rrad = 4, vrad=.5)
{
    hull()
    {
        translate([ ((width/2) - rrad), ((depth/2) - rrad), 0])  
            roundedCylinder(height=height, rrad = rrad, vrad = vrad);
        
        translate([-((width/2) - rrad), ((depth/2) - rrad), 0])  
            roundedCylinder(height=height, rrad = rrad, vrad = vrad);

        translate([ ((width/2) - rrad), -((depth/2) - rrad), 0]) 
            roundedCylinder(height=height, rrad = rrad, vrad = vrad);

        translate([-((width/2) - rrad), -((depth/2) - rrad), 0]) 
            roundedCylinder(height=height, rrad = rrad, vrad = vrad);
    }
}

module roundedCylinder(height=2.8, rrad = 4, vrad=.5)
{
    hull()
    {
        translate([ 0, 0,          vrad]) ring(rrad = rrad, vrad = vrad);
        translate([ 0, 0, height - vrad]) ring(rrad = rrad, vrad = vrad);
    }
}

module ring(rrad = 4, vrad=.5)
{
    rotate_extrude() translate([rrad - vrad, 0, 0])  circle(r = vrad);
}


////////////////////////////////
module brick_h(	height                  = 15, 
				width                   = 15, 
				depth                   = 15, 
				leftAxes_Vertical       = [], 
                leftAxes_Horizontal     = [],
				rightAxes_Vertical      = [],
				rightAxes_Horizontal    = [],
				bottomAxesFB            = [], 
				topAxesFB               = [],
				bottomAxesLR            = [], 
				topAxesLR               = [],
                frontAxes_Vertical      = [], 
                frontAxes_Horizontal    = [], 
                backAxes_Vertical       = [],
                backAxes_Horizontal     = [],
                glidersFB_Vertical      = [],
                glidersFB_Horizontal    = [],
                glidersLR_Vertical      = [],
                glidersLR_Horizontal    = [],
                squareHoles             = false,
                triangleAxes            = false)
{
	difference()
	{
		brick(	height,	width, depth, 
				leftAxes_Vertical		= leftAxes_Vertical,
				leftAxes_Horizontal	    = leftAxes_Horizontal,
                rightAxes_Vertical 	    = rightAxes_Vertical,
                rightAxes_Horizontal 	= rightAxes_Horizontal,
				bottomAxesFB       	    = bottomAxesFB,
                topAxesFB	            = topAxesFB,
				bottomAxesLR	        = bottomAxesLR,
                topAxesLR	            = topAxesLR,
				frontAxes_Vertical		= frontAxes_Vertical,
                frontAxes_Horizontal    = frontAxes_Horizontal, 
                backAxes_Vertical	    = backAxes_Vertical, 
                backAxes_Horizontal     = backAxes_Horizontal,
                squareHoles             = squareHoles,
                triangleAxes            = triangleAxes);

        for(i = glidersLR_Vertical)
        {
            translate([ 0, 0, i * 7.5 + height/2])
                rotate([0, 90, 0])
                cylinder(d=gliderAsDiameter, h=width + 2, center=true);
        }

        for(i = glidersLR_Horizontal)
        {
            translate([ 0, -((i * 7.5) + (depth/2)), height/2])
                rotate([0, 90, 0])
                cylinder(d=gliderAsDiameter, h=width + 2, center=true);
        }

        for(i = glidersFB_Vertical)
        {
            translate([ 0, 0, i * 7.5 + height/2])
                rotate([90, 0, 0])
                cylinder(d=gliderAsDiameter, h=depth + 2, center=true);
        }

        for(i = glidersFB_Horizontal)
        {
            translate([ i * 7.5, 0, height/2])
                rotate([90, 0, 0])
                cylinder(d=gliderAsDiameter, h=depth + 2, center=true);
        }

        for(i = glidersLR_Vertical)
        {
            translate([ 0, i * 7.5, height/2])
                cylinder(d=gliderAsDiameter, h=height + 2, center=true);
        }

        for(i = glidersLR_Horizontal)
        {
            translate([ i * 7.5, 0, height/2])
                cylinder(d=gliderAsDiameter, h=height + 2, center=true);
        }
	}
}


module roundedCube(x, y, z, rrect=rrect)
{
    translate([-x/2, -y/2, 0])
    hull() {
        translate([rrect, rrect, rrect])             sphere(r = rrect);
        translate([x - rrect, rrect, rrect])         sphere(r = rrect);

        translate([rrect, y - rrect, rrect])         sphere(r = rrect);
        translate([x - rrect, y - rrect, rrect])     sphere(r = rrect);

        translate([rrect, rrect, z - rrect])         sphere(r = rrect);
        translate([x - rrect, rrect, z - rrect])     sphere(r = rrect);

        translate([rrect, y - rrect, z - rrect])     sphere(r = rrect);
        translate([x - rrect, y - rrect, z - rrect]) sphere(r = rrect);
    }
}


module brick(	height                  =15, 
				width                   =15, 
				depth                   =15, 
				leftAxes_Vertical      =[], 
				leftAxes_Horizontal    =[], 
				rightAxes_Vertical     =[],
				rightAxes_Horizontal   =[],
				bottomAxesFB           =[], 
				topAxesFB              =[],
				bottomAxesLR           =[], 
				topAxesLR              =[],
                frontAxes_Vertical     =[], 
                frontAxes_Horizontal   =[], 
                backAxes_Vertical      =[],
                backAxes_Horizontal    =[],
                squareHoles             =false,
                triangleAxes            =false)
{
	intersection() 
	{
		roundedCube(width, depth, height);
		translate([0, 0, height/2])
		{
			difference()
			{
				cube([width, depth, height], center=true);

                fisherAxes([  0,  0, 270],  depth/15,   height,     frontAxes_Vertical,     triangleAxes);
                fisherAxes([  90,  0, 270], depth/15,   width,      frontAxes_Horizontal,   triangleAxes);
                fisherAxes([  0,  0,  90],  depth/15,   height,     -backAxes_Vertical,     triangleAxes);
                fisherAxes([  90,  0, 90],  depth/15,   width,      backAxes_Horizontal,    triangleAxes);
                fisherAxes([  0, 270, 90],  height/15,  depth,      -topAxesFB,             triangleAxes);
                fisherAxes([  0,  90, 90],  height/15,  depth,      -bottomAxesFB,          triangleAxes);
                fisherAxes([  0, 270, 0],   height/15,  width,      topAxesLR,              triangleAxes);
                fisherAxes([  0,  90, 180], height/15,  width,      -bottomAxesLR,          triangleAxes);
                fisherAxes([  0, 180,  0],  width/15,   height,     leftAxes_Vertical,      triangleAxes);
                fisherAxes([  270, 180, 0], width/15,   depth,      leftAxes_Horizontal,      triangleAxes);
                fisherAxes([  0,  0,   0],  width/15,   height,     rightAxes_Vertical,     triangleAxes);
                fisherAxes([  90,  0,   0], width/15,   depth,      rightAxes_Horizontal,   triangleAxes);

				if(squareHoles) {
					edgeDeltaW=((width/2)-2.19);
					edgeDeltaH=((height/2)-2.19);
					edgeDeltaD=((depth/2)-2.19);
					
					for(i = modulo(rightAxes_Vertical, topAxesLR) ){
						translate([edgeDeltaW, i*7.5, edgeDeltaH])	
							edgeBlock();
					}

					for(i = modulo(rightAxes_Vertical, bottomAxesLR) ){
						translate([edgeDeltaW, i*7.5, -edgeDeltaH])	
							edgeBlock();
					}

					for(i = modulo(leftAxes_Vertical, topAxesLR) ){
						translate([-edgeDeltaW, i*7.5, (height/2)-2.19])	
							edgeBlock();
					}

					for(i = modulo(leftAxes_Vertical, bottomAxesLR) ){
						translate([-edgeDeltaW, i*7.5 , -edgeDeltaH])	
							edgeBlock();
					}

					for(i = modulo(frontAxes_Vertical, topAxesFB) ){
						translate([i * 7.5, -edgeDeltaD, edgeDeltaH])	
							edgeBlock();
					}

					for(i = modulo(frontAxes_Vertical, bottomAxesFB) ){
						translate([i * 7.5, -edgeDeltaD, -edgeDeltaH])	
							edgeBlock();
					}

					for(i = modulo(frontAxes_Horizontal, leftAxes_Horizontal) ){
						translate([-edgeDeltaW, -edgeDeltaD, i * 7.5])	
							edgeBlock();
					}
					for(i = modulo(backAxes_Horizontal, leftAxes_Horizontal) ){
						translate([-edgeDeltaW, edgeDeltaD, i * 7.5])	
							edgeBlock();
					}

					for(i = modulo(backAxes_Vertical, topAxesFB) ){
						translate([i * 7.5, edgeDeltaD, edgeDeltaH])	
							edgeBlock();
					}

					for(i = modulo(backAxes_Vertical, bottomAxesFB) ){
						translate([i * 7.5, edgeDeltaD, -edgeDeltaH])	
							edgeBlock();
					}
					for(i = modulo(frontAxes_Horizontal, rightAxes_Horizontal) ){
						translate([edgeDeltaW, -edgeDeltaD, i * 7.5])	
							edgeBlock();
					}
					for(i = modulo(backAxes_Horizontal, rightAxes_Horizontal) ){
						translate([edgeDeltaW, edgeDeltaD, i * 7.5])	
							edgeBlock();
					}
				}
			}
		}
	}
}

module edgeBlock()
{
    cube([4.4, 4.4, 4.4], center=true);
}

module fisherAxes(rot, trans, h, axes, triangleAxes=false)
{
    for(i = axes)
    {
        fisherAxis( rot, trans, h, i, triangleAxes );
    }
}

module fisherAxis( rot, trans, h, t2=0, triangleAxes=false )
{
    rotate(rot)
    translate([7.5, 0, 0] * trans)
    translate([0, 7.5, 0] * t2)
        if( triangleAxes) {
            triangleMount(length = h + 5);
        }
        else
        {
                fisherAxisBase(h=h+5);
        }
}

module fisherAxisBase(h)
{
    union()
    {
        translate([-2.25, 0, 0] )
            cylinder(d=fisherAsDiameter, h=h, center=true);
        translate([-0.4, 0, 0])
            cube([1, 3, h], center=true);
    }
}


module triangleMount(length = 15, overlap=0.01, tolerance = 0.05) {
    translate([0, 0, -length/2])
    linear_extrude(height=length)
        polygon( [
            [ -2.9,                    -2.05 ],
            [ -2.7,                  -2.05 ],
            [ -.6,  -1.5],
            [ 0.1,  -1.5],
            [ 0.1,  1.5],
            [ -.6,  1.5],
            [ -2.7,                  2.05 ],
            [ -2.9,                    2.05 ]
        ]);
}
	

/////////////////////


module fastnerBorg(totalHeight=fastnerDoubleHeight)
{
    difference()
    {
        cylinder(d=fastnerDia, h=totalHeight);

        translate([0, 0, -1])
        cylinder(d=fisherAsDiameter, h=totalHeight+2);
        
        rotate([0, 0, 90]) borgRing();
        
        translate([0, 0, totalHeight])
            rotate([0, 180, 90]) fastner();
       
    }
}

module doubleFastner(totalHeight=fastnerDoubleHeight)
{
    difference()
    {
        cylinder(d=fastnerDia, h=totalHeight);

        translate([0, 0, -1]) cylinder(d=fisherAsDiameter, h=totalHeight+2);
        
        fastner();

        translate([0, 0, totalHeight])
            rotate([0, 180, 90]) fastner();
    }
}

module fastner(fastnerHeight=fastnerHeight)
{
    hull() {
        translate([0, 0, 6]) cube([25, 2, 1], center=true);
        translate([0, 0, .000]) cube([25, 3, .01], center=true);
    }

    translate([0, 0, -0.01])
    cylinder(d1=14.5, d2=12.0, h=5.57);

    translate([0, 0, -1]) cylinder(d=12.0, h=7);
}

module borgRing()
{
    union() {
        cylinder(d=8, h=13, center=true);
        translate([0, 0, 2.5]) rotate([90, 0, 0]) cylinder(d=3, h=15);
    }
}

module roundMount(blen=15, pllen=6.8, ploffs=0) {
    union() {
        if ( pllen > 0)
            translate([0, ploffs, 0.15])
                cube([3., pllen, 0.3], center=true);
        if ( pllen == 0)
            translate([0, ploffs, 0.01])
                cube([3., blen, 0.05], center=true);

        translate([0, blen/2, 0]) 
            rotate([90, 0, 0])
                linear_extrude(height=blen)
                    difference() {
                        union() {
                            translate([0, -2.05])
                                circle(d=4.06);

                            translate([0, -1.5])
                                square([3, 3], center=true);
                        }

                        translate([0, -3.6])
                            square([5, 2], center=true);
                        
                        translate([0, -2.0])
                            square([1, 2], center=true);
                    }
    }
}

module wpin()
{
    union() {
        difference() {
            hull() {
                translate([0, 0, 2.05])
                    cylinder(d=4, h=3.55, center=true);
                translate([0, 0, 4.06])
                    cylinder(d1=4., d2=3, h=.5, center=true);
            }
            translate([ 2.5, 0, 1.65]) cube([2., 4, 2.8], center=true);
            translate([-2.5, 0, 1.65]) cube([2., 4, 2.8], center=true);
        }
        hull() {
            translate([0, 0, 4.25]) cube([4.2, 2., 0.1], center=true);
            translate([0, 0, 3.75]) cube([5.2, 3., 0.1], center=true);
            translate([0, 0, 3.45]) cube([5.8, 3., 0.1], center=true);
            translate([0, 0, 2.65]) cube([5.8, 3., 0.1], center=true);
            translate([0, 0, 1.55]) cube([3.1, 3., 0.1], center=true);
        }
    }
}

module pin_s_9_5()
{
    union() {
        translate([0, -2.675, 0]) 
            roundMount(blen=9.45, pllen=0, ploffs=0);
        pin();
    }
}

module pin_s_15()
{
    union() {
        roundMount(blen=15, pllen=0, ploffs=0);
        pin();
    }
}

module pin_h_9_5() {
    union() {
        translate([0, -2.675, 0]) 
            roundMount(blen=9.45, pllen=5.45, ploffs=2);
        wpin();
    }
}

module pin_h_15() {
    union() {
        roundMount(blen=15, pllen=6.8, ploffs=0);
        wpin();
    }
}


module pin_r_9_5() {
    union() {
        translate([0, -2.675, 0]) 
            roundMount(blen=9.45, pllen=0, ploffs=0);
        rpin();
    }
}

module pin_r_15() {
    union() {
        roundMount(blen=15, pllen=0, ploffs=0);
        rpin();
    }
}

module roundedRect(width, depth, rad)
{
    hull() {
        translate([   width/2 - rad, depth/2 - rad]) circle(r = rad);
        translate([ -(width/2 - rad), depth/2 - rad]) circle(r = rad);
        translate( [-(width/2 - rad), -(depth/2 - rad)]) circle(r = rad);
        translate([   width/2 - rad,  -(depth/2 - rad)]) circle(r = rad);
    }
}

module pin()
{
    translate([0, 0, 0])
    roundedCube(2.8, 2.8, 1, .2);

    ddd = 1.81;
    ddh = 2.36;
    dds = .14;
    dda=.45;
    ddx = 1.37;
    hull()
    for(a = [0:1:19])
    {
        rr = dda + sin(3.4*a+35) * ddx;
        translate([rr, rr, .7+a/11.5]) cubic(dds*2);
        translate([rr, -rr, .7+a/11.5]) cubic(dds*2);
        translate([-rr, -rr, .7+a/11.5]) cubic(dds*2);
        translate([-rr, rr, .7+a/11.5]) cubic(dds*2);
    }
}

module cubic(r)
{
    cube([r, r, r], center=true);
}


module rpin() {
    union() {
        difference() {
            translate([.0, 0, 2.05])
                sphere(r=2.025);
            translate([0, 0, 3.60])
                cube([4.5,4.5, 2], center=true);
        }
        translate([0, 0, .285]) cylinder(r=1.4, h=.6, center=true);    
    }
}


module roundedBatten(r=30)
{
    rr = round(r*10)/10;
    s2 = (r < 20) ? "" : (r == rr) ?  " --" : " X";
    s1 = str(rr);
    s=str(s1, s2);
    difference() {
        staticLatRounded(width=r );
        
        translate([  r/2,  0, 0]) staticGap(angle=0);
        translate([  -r/2,  0, 0]) staticGap(angle=0);
        
        translate([5 -r/2, -1.5, 2.4])
            linear_extrude(height = 0.5)
                text(s, size=3, font="Arial:style=Regular");
        
    }
}

module batten(r=30, angle=0, text=undef)
{
    rr = round(r*10)/10;
    
    s2 = (r < 20) ? "" : (r == rr) ?  " --" : " X";
    s1 = str(rr);
    s = (text==undef) ? str(s1, s2) : text;
    difference() {
        staticLat(width=r );
        
        translate([  r/2,  0, 0]) staticGap(angle=angle);
        translate([  -r/2,  0, 0]) staticGap(angle=angle);
        
        translate([5 -r/2, -1.5, 2.4])
            linear_extrude(height = 0.5)
                text(s, size=3, font="Arial:style=Regular");
        
    }
}

module staticGap(heightTop=10, heightBottom=0, angle=0) 
{
    translate([0, 0, heightTop/2 + staticMountHeigth])
        cylinder(d=6.8, h=heightTop, center=true);

    if ( heightBottom > 0)
        translate([0, 0, -heightBottom/2])
            cylinder(d=6.8, h=heightBottom, center=true);
    
    cylinder(d=4.5, h=heightTop + heightBottom + staticMountHeigth, center=true);
    
    intersection() {
        rotate([0, 0, angle])
            cube([10, 3.6, heightTop + heightBottom + staticMountHeigth], center=true);
        cylinder(d=6.9, h=heightTop + heightBottom + staticMountHeigth, center=true);
    }
}


module staticLat(width=30, depth=0, staticHeight=staticHeight, staticLatRad = staticLatRad, vrad=.5)
{
    translate([-width/2, 0, staticHeight/2])
        cylinder(d=staticLatDia, h=staticHeight, center=true);
    translate([width/2, 0, staticHeight/2])
        cylinder(d=staticLatDia, h=staticHeight, center=true);
    translate([0, 0, staticHeight/2])
        cube([width, depth+staticLatDia, staticHeight], center=true);
}

module staticLatRounded(width=30, depth=0, staticHeight=staticHeight, staticLatRad = staticLatRad, vrad=.5)
{
    roundedPlate(width=width+staticLatDia, depth=depth+staticLatDia, height=staticHeight, rrad = staticLatRad, vrad=vrad);
}



module brick_1_1(height=15)
{
	brick(	height,
			15, 15, 
			leftAxes_Vertical=even_1,		rightAxes_Vertical=oddeven_1,
			bottomAxesFB=oddeven_1,	        topAxesFB=oddeven_1,
			bottomAxesLR=[],			    topAxesLR=[],
			frontAxes_Vertical=oddeven_1,	backAxes_Vertical=oddeven_1,
			squareHoles=true );
}

module brick_1_2(height=15)
{
	brick(	height,
			30, 15, 
			leftAxes_Vertical=oddeven_1,	rightAxes_Vertical=oddeven_1,
			bottomAxesFB=oddeven_2,	        topAxesFB=oddeven_2,
			bottomAxesLR=[],			    topAxesLR=[],
			frontAxes_Vertical=oddeven_2,	backAxes_Vertical=oddeven_2,
			squareHoles=true );
}

module brick_1_3(height=15)
{
	brick(	height,
			45, 15, 
			leftAxes_Vertical=oddeven_1,	rightAxes_Vertical=oddeven_1,
			bottomAxesFB=oddeven_3,	        topAxesFB=oddeven_3,
			bottomAxesLR=[],			    topAxesLR=[],
			frontAxes_Vertical=oddeven_3,	backAxes_Vertical=oddeven_3,
			squareHoles=true );
}
 
module brick_1_4(height=15)
{
	brick(	height,
			60, 15, 
			leftAxes_Vertical=oddeven_1,	rightAxes_Vertical=oddeven_1,
			bottomAxesFB=oddeven_4,	        topAxesFB=oddeven_4,
			bottomAxesLR=[],			    topAxesLR=[],
			frontAxes_Vertical=oddeven_4,	backAxes_Vertical=oddeven_4,
			squareHoles=true );
}

module brick_1_5(height=15)
{
	brick(	height,
			75, 15,  
			leftAxes_Vertical=oddeven_1,	rightAxes_Vertical=oddeven_1,
			bottomAxesFB=oddeven_5,	        topAxesFB=oddeven_5,
			bottomAxesLR=[],			    topAxesLR=[],
			frontAxes_Vertical=oddeven_5,	backAxes_Vertical=oddeven_5,
			squareHoles=true );
}

module brick_1_6(height=15)
{
	brick(	height,
			90, 15,  
			leftAxes_Vertical=oddeven_1,	rightAxes_Vertical=oddeven_1,
			bottomAxesFB=oddeven_6,	        topAxesFB=oddeven_6,
			bottomAxesLR=[],			    topAxesLR=[],
			frontAxes_Vertical=oddeven_6,	backAxes_Vertical=oddeven_6,
			squareHoles=true );
}

module brick_1_7(height=15)
{
	brick(	height,
			105, 15, 
			leftAxes_Vertical=oddeven_1,	rightAxes_Vertical=oddeven_1,
			bottomAxesFB=oddeven_7,	        topAxesFB=oddeven_7,
			bottomAxesLR=[],			    topAxesLR=[],
			frontAxes_Vertical=oddeven_7,	backAxes_Vertical=oddeven_7,
			squareHoles=true );
}

module brick_1_8(height=15)
{
	brick(	height,
			120, 15, 
			leftAxes_Vertical=oddeven_1,	rightAxes_Vertical=oddeven_1,
			bottomAxesFB=oddeven_8,	        topAxesFB=oddeven_8,
			bottomAxesLR=[],			    topAxesLR=[],
			frontAxes_Vertical=oddeven_8,	backAxes_Vertical=oddeven_8,
			squareHoles=true );
}

/////////////////////////////

module brick_2_2(height=15)
{
	brick(	height,
			30, 30, 
			leftAxes_Vertical=oddeven_2,	rightAxes_Vertical=oddeven_2,
			bottomAxesFB=oddeven_2,	        topAxesFB=oddeven_2,
			bottomAxesLR=[],			    topAxesLR=[],
			frontAxes_Vertical=oddeven_2,	backAxes_Vertical=oddeven_2,
			squareHoles=true );
}

module brick_2_3(height=15)
{
	brick(	height,
			45, 30, 
			leftAxes_Vertical=oddeven_2,	rightAxes_Vertical=oddeven_2,
			bottomAxesFB=oddeven_3,	        topAxesFB=oddeven_3,
			bottomAxesLR=[],			    topAxesLR=[],
			frontAxes_Vertical=oddeven_3,	backAxes_Vertical=oddeven_3,
			squareHoles=true );
}

module brick_2_4(height=15)
{
	brick(	height,
			60, 30, 
			leftAxes_Vertical=oddeven_2,	rightAxes_Vertical=oddeven_2,
			bottomAxesFB=oddeven_4,	        topAxesFB=oddeven_4,
			bottomAxesLR=[],			    topAxesLR=[],
			frontAxes_Vertical=oddeven_4,	backAxes_Vertical=oddeven_4,
			squareHoles=true );
}

module brick_2_5(height=15)
{
	brick(	height,
			75, 30, 
			leftAxes_Vertical=oddeven_2,	rightAxes_Vertical=oddeven_2,
			bottomAxesFB=oddeven_5,	        topAxesFB=oddeven_5,
			bottomAxesLR=[],			    topAxesLR=[],
			frontAxes_Vertical=oddeven_5,	backAxes_Vertical=oddeven_5,
			squareHoles=true );
}

module brick_2_6(height=15)
{
	brick(	height,
			90, 30, 
			leftAxes_Vertical=oddeven_2,	rightAxes_Vertical=oddeven_2,
			bottomAxesFB=oddeven_6,	        topAxesFB=oddeven_6,
			bottomAxesLR=[],			    topAxesLR=[],
			frontAxes_Vertical=oddeven_6,	backAxes_Vertical=oddeven_6,
			squareHoles=true );
}

module brick_2_7(height=15)
{
	brick(	height,
			105, 30, 
			leftAxes_Vertical=oddeven_2,	rightAxes_Vertical=oddeven_2,
			bottomAxesFB=oddeven_7,	        topAxesFB=oddeven_7,
			bottomAxesLR=[],			    topAxesLR=[],
			frontAxes_Vertical=oddeven_7,	backAxes_Vertical=oddeven_7,
			squareHoles=true );
}

module brick_2_8(height=15)
{
	brick(	height,
			120, 30, 
			leftAxes_Vertical=oddeven_2,	rightAxes_Vertical=oddeven_2,
			bottomAxesFB=oddeven_8,	        topAxesFB=oddeven_8,
			bottomAxesLR=[],			    topAxesLR=[],
			frontAxes_Vertical=oddeven_8,	backAxes_Vertical=oddeven_8,
			squareHoles=true );
}

//////////////////

module brick_1_4_g(height=15, 
					gliderAsDiameter=6)
{
	difference()
	{
		brick(	height,
				60, 15, 
				leftAxes_Vertical		= oddeven_1,	rightAxes_Vertical	= oddeven_1,
				bottomAxesFB	= [0],			        topAxesFB	= [0],
				bottomAxesLR	= [],			        topAxesLR	= [],
				frontAxes_Vertical		= even_4,		backAxes_Vertical	= even_4);

        translate([ 15, 0, height/2]) 
            cylinder(d=gliderAsDiameter, h=height + 2, center=true);
        translate([-15, 0, height/2]) 
            cylinder(d=gliderAsDiameter, h=height + 2, center=true);

	}
}

module brick_1_6_g(height=15, gliderAsDiameter=6)
{
	difference()
	{
		brick(	height,
				90, 15, 
				leftAxes_Vertical=oddeven_1,	rightAxes_Vertical=oddeven_1,
				bottomAxesFB=[-1, 1],	        topAxesFB=[-1, 1],
				bottomAxesLR=[],		        topAxesLR=[],
				frontAxes_Vertical=even_6,		backAxes_Vertical=even_6,
                squareHoles=true );

        translate([ 30, 0, height/2]) 
            cylinder(d=gliderAsDiameter, h=height + 2, center=true);
        translate([-30, 0, height/2]) 
            cylinder(d=gliderAsDiameter, h=height + 2, center=true);
	}
}

module brick_1_8_g(height=15, gliderAsDiameter=6)
{
	difference()
	{
		brick(	height,
				120, 15, 
				leftAxes_Vertical=oddeven_1,		rightAxes_Vertical=oddeven_1,
				bottomAxesFB=[],			topAxesFB=[],
				bottomAxesLR=[],			topAxesLR=[],
				frontAxes_Vertical=even_8,			backAxes_Vertical=even_8);

        translate([ 45, 0, height/2])
            cylinder(d=gliderAsDiameter, h=height + 2, center=true);
        translate([-45, 0, height/2]) 
            cylinder(d=gliderAsDiameter, h=height + 2, center=true);
	}
}
